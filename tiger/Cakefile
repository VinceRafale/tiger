fs     = require 'fs'
{exec} = require 'child_process'

appFiles  = [
  # omit src/ and .coffee to make the below lines a little shorter
  'models'
  'views/sections'
  'views/items'
  'views/cart'
  'controllers/menu_controller'
  'application'
]

task 'build', 'Build single application file from source files', ->
  appContents = new Array remaining = appFiles.length
  for file, index in appFiles then do (file, index) ->
    fs.readFile "media/js/menu/#{file}.coffee", 'utf8', (err, fileContents) ->
      throw err if err
      appContents[index] = fileContents
      process() if --remaining is 0
  process = ->
    fs.writeFile 'media/js/menu.coffee', appContents.join('\n\n'), 'utf8', (err) ->
      throw err if err
      exec 'coffee -o media/js --compile media/js/menu.coffee', (err, stdout, stderr) ->
        throw err if err
        fs.unlink 'media/js/menu.coffee', (err) ->
          throw err if err

task 'watch', 'Watch source files for changes and invoke build', ->
    for file in appFiles then do (file) ->
        fs.watchFile "media/js/menu/#{file}.coffee", (curr, prev) ->
            if +curr.mtime isnt +prev.mtime
                invoke 'build'
